plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'java'
apply plugin: 'idea'

group 'com.senither'
version '0.1-SNAPSHOT'
mainClassName = 'com.senither.hypixel.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    moduleName = 'Hypixel Skyblock Assistant'
}

shadowJar {
    archiveName = "HypixelSkyblockAssistant.jar"

    doLast {
        copy {
            from 'build/libs/HypixelSkyblockAssistant.jar'
            into '.'
        }
    }
}

tasks.withType(JavaCompile) {
    dependsOn 'clean'
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:-unchecked"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'HypixelSkyblockAssistant: Discord Bot',
            'Implementation-Version': version,
            'Main-Class': mainClassName
    }

    baseName = project.name

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile group: 'net.dv8tion', name: 'JDA', version: '4.1.0_88'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'com.github.Senither.PublicAPI', name: 'Example', version: '9605878150'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.18'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
            "project.version"   : project.version,
            "project.groupId"   : project.group,
            "project.artifactId": project.ext.moduleName
        ]
    }
}
